programa : lista_declaracoes

lista_declaracoes : lista_declaracoes declaracao
                  | declaracao

declaracao : declaracao_variaveis
                             | inicializacao_variaveis
                             | declaracao_funcao

declaracao_variaveis : tipo DOISPONTOS lista_variaveis

inicializacao_variaveis : atribuicao

lista_variaveis : lista_variaveis VIRGULA var
                | var

var : ID
    | ID indice

indice : indice LCOLCHETE expressao RCOLCHETE
       | LCOLCHETE expressao RCOLCHETE

tipo : INTEIRO
    | FLUTUANTE

declaracao_funcao : tipo cabecalho
                  | cabecalho

cabecalho : ID LPARENTHESYS lista_parametros RPARENTHESYS corpo FIM

lista_parametros : lista_parametros VIRGULA parametro
                 | parametro
                 | vazio

parametro : tipo DOISPONTOS ID
          | parametro LCOLCHETE RCOLCHETE

corpo : corpo acao
      | vazio

acao : expressao
     | declaracao_variaveis
     | se
     | repita
     | leia
     | escreva
     | retorna
     | error

se : SE expressao ENTAO corpo FIM
   | SE expressao ENTAO corpo SENAO corpo FIM

repita : REPITA corpo ATE expressao

atribuicao : var ATRIB expressao

leia : LEIA LPARENTHESYS var RPARENTHESYS

escreva : ESCREVA LPARENTHESYS expressao RPARENTHESYS

retorna : RETORNA LPARENTHESYS expressao RPARENTHESYS

expressao : expressao_logica
          | atribuicao

expressao_logica : expressao_simples
                 | expressao_logica operador_logico expressao_simples

expressao_simples : expressao_aditiva
                  | expressao_simples operador_relacional expressao_aditiva

expressao_aditiva : expressao_multiplicativa
                  | expressao_aditiva operador_soma expressao_multiplicativa

expressao_multiplicativa : expressao_unaria
                         | expressao_multiplicativa operador_multiplicacao expressao_unaria

expressao_unaria : fator
                 | operador_soma fator
                 | operador_negacao fator

operador_relacional : LESSER
                    | GREATER
                    | EQUAL
                    | DIFF
                    | LESSEREQ
                    | GREATEREQ

operador_soma : PLUS
              | MINUS

operador_negacao : NEGATION

operador_logico : AND
                | OR

operador_multiplicacao : MULT
                       | DIV

fator : LPARENTHESYS expressao RPARENTHESYS
      | var
      | chamada_funcao
      | numero

numero : INT
       | FLOAT

chamada_funcao : ID LPARENTHESYS lista_argumentos RPARENTHESYS

lista_argumentos : lista_argumentos VIRGULA expressao
                 | expressao
                 | vazio

vazio :
